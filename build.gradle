plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'

    // SonarQube plugin
    id 'org.sonarqube' version '4.4.1.3373'

    // Publishing to Nexus
    id 'maven-publish'

    id 'jacoco'
}

jacoco {
    toolVersion = "0.8.10"
}

group = 'com.vacancy'
version = '0.0.2-SNAPSHOT'


java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Hibernate utils
    implementation 'com.vladmihalcea:hibernate-types-60:2.21.1'
    implementation "io.hypersistence:hypersistence-utils-hibernate-63:3.9.0"

    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'

    // Logging
    implementation 'org.springframework.boot:spring-boot-starter-logging'

    testImplementation 'io.qameta.allure:allure-junit5:2.23.0'
    testImplementation 'io.qameta.allure:allure-java-commons:2.23.0'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

tasks.named('test') {
    useJUnitPlatform()
    systemProperty "allure.results.directory", "build/allure-results"
    finalizedBy jacocoTestReport
}



tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(Test) {
    systemProperty "file.encoding", "UTF-8"
}

tasks.withType(Jar) {
    enabled = true
}

// -----------------------------
//     SonarQube configuration
// -----------------------------
sonar {
    properties {
        property "sonar.projectKey", "my-app-devops-ex-key"
        property "sonar.projectName", "my-app-devops-ex-name"
        property "sonar.host.url", "http://sonarqube:9000"
    }
}

// -----------------------------
//     Publishing (Nexus)
// -----------------------------
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact("${buildDir}/libs/${rootProject.name}-${version}.jar")
        }
    }

    repositories {
        maven {
            name = "nexus"
            url = uri("http://nexus:8081/repository/maven-snapshots/")
            allowInsecureProtocol = true
            credentials {
                username = project.findProperty("nexusUser")
                password = project.findProperty("nexusPass")
            }
        }
    }
}